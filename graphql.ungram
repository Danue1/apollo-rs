// Legend:
//
//   //          -- comment
//   Name =      -- non-terminal definition
//   'ident'     -- token (terminal)
//   A B         -- sequence
//   A | B       -- alternation
//   A*          -- zero or more repetition
//   A?          -- zero or one repetition
//   (A)         -- same as A
//   label:A     -- suggested name for field of AST node

Name =
  'ident'

///////////////////////////
//        DOCUMENT       //
///////////////////////////
Document =
  Definition*

Definition =
  OperationDefinition
  | FragmentDefinition
  | DirectiveDefinition
  | SchemaDefinition
  // Type Definitions
  | ScalarTypeDefinition
  | ObjectTypeDefinition
  | InterfaceTypeDefinition
  | UnionTypeDefinition
  | EnumTypeDefinition
  | InputObjectTypeDefinition
  | SchemaExtension
  | TypeExtension

OperationDefinition =
  OperationType Name? VariableDefinitions? Directives? SelectionSet
  | SelectionSet

OperationType =
  'query'
  | 'mutation'
  | 'subscription'

SelectionSet =
  '{' Selection* '}'

Selection =
  Field
  | FragmentSpread
  | InlineFragment

Field =
  Alias? Name Arguments? Directives? SelectionSet?

Alias =
  Name ':'

Arguments =
  '(' Argument* ')'

Argument =
  Name ':' Value

FragmentSpread =
  '...' FragmentName Directives?

InlineFragment =
  '...' TypeCondition? Directives? SelectionSet

FragmentDefinition =
  'fragment' FragmentName TypeCondition Directives? SelectionSet

FragmentName =
  Name

TypeCondition =
  'on' NamedType

Value =
  Variable
  | StringValue
  | FloatValue
  | IntValue
  | BooleanValue
  | NullValue
  | EnumValue
  | ListValue
  | ObjectValue

StringValue =
  'string_value'

FloatValue =
  'float_value'

IntValue =
  'int_value'

BooleanValue =
  'true'
  | 'false'

NullValue =
  'null'

EnumValue =
  Name

ListValue =
  '[' ']'
  '[' Value* ']'

ObjectValue =
 '{' '}'
 '{' ObjectField* '}'

ObjectField =
  Name ':' Value

VariableDefinitions =
  '(' VariableDefinition* ')'

VariableDefinition =
  Variable ':' Type DefaultValue?

Variable =
  '$' Name

DefaultValue =
  '=' Value

Type =
  NamedType
  | ListType
  | NonNullType

NamedType =
  Name

ListType =
  '[' Type ']'

NonNullType =
  NamedType '!'
  | ListType '!'

Directives =
  Directive*

Directive =
  '@' Name Arguments?

SchemaDefinition =
  'schema' Directives? '{' OperationTypeDefinition* '}'

SchemaExtension =
  'extend' 'schema' Directives? '{' OperationTypeDefinition* '}'
  | 'extend' 'schema' Directives

OperationTypeDefinition =
  OperationType ':' NamedType

Description =
  StringValue

// TODO @lrlna: we are creating an extra type here to make it easier for codegen.
TypeExtension =
  TypeExtensionKinds

TypeExtensionKinds =
  ScalarTypeExtension
  | ObjectTypeExtension
  | InterfaceTypeExtension
  | UnionTypeExtension
  | EnumTypeExtension
  | InputObjectTypeExtension

ScalarTypeDefinition =
  Description? 'scalar' Name Directives?

ScalarTypeExtension =
  'extend' 'scalar' Name Directives

ObjectTypeDefinition =
  Description? 'type' Name ImplementsInterfaces? Directives? FieldsDefinition?

ObjectTypeExtension =
  'extend' 'type' Name ImplementsInterfaces? Directives? FieldsDefinition
  | 'extend' 'type' Name ImplementsInterfaces? Directives
  | 'extend' 'type' Name ImplementsInterfaces

ImplementsInterfaces =
  'implements' '&'? NamedType
  | ImplementsInterfaces '&' NamedType

FieldsDefinition =
  '{' FieldDefinition* '}'

FieldDefinition =
  Description? Name ArgumentsDefinition? ':' Type Directives?

ArgumentsDefinition =
  '(' InputValueDefinition* ')'

InputValueDefinition =
  Description? Name ':' Type DefaultValue? Directives?

InterfaceTypeDefinition =
  Description? 'interface' Name Directives? FieldsDefinition?

InterfaceTypeExtension =
  'extend' 'interface' Name Directives? FieldsDefinition
  | 'extend' 'interface' Name Directives

UnionTypeDefinition =
  Description? 'union' Name Directives? UnionMemberTypes?

UnionMemberTypes =
  '=' '|'? NamedType
  | UnionMemberTypes '|' NamedType

UnionTypeExtension =
  'extend' 'union' Name Directives? UnionMemberTypes
  | 'extend' 'union' Name Directives

EnumTypeDefinition =
  Description? 'enum' Name Directives? EnumValuesDefinition?

EnumValuesDefinition =
  '{' EnumValueDefinition* '}'

EnumValueDefinition =
  Description? EnumValue Directives?

EnumTypeExtension =
  'extend' 'enum' Name Directives? EnumValuesDefinition
  | 'extend' 'enum' Name Directives

InputObjectTypeDefinition =
  Description? 'input' Name Directives? InputFieldsDefinition?

InputFieldsDefinition =
  '{' InputValueDefinition* '}'

InputObjectTypeExtension =
  'extend' 'input' Name Directives? InputFieldsDefinition
  | 'extend' 'input' Name Directives?

DirectiveDefinition =
  Description? 'directive' '@' Name ArgumentsDefinition? 'on' DirectiveLocations

DirectiveLocations =
  '|'? DirectiveLocation
  | DirectiveLocations '|' DirectiveLocation

DirectiveLocation =
  'QUERY'
  | 'MUTATION'
  | 'SUBSCRIPTION'
  | 'FIELD'
  | 'FRAGMENT_DEFINITION'
  | 'FRAGMENT_SPREAD'
  | 'INLINE_FRAGMENT'
  | 'SCHEMA'
  | 'SCALAR'
  | 'OBJECT'
  | 'FIELD_DEFINITION'
  | 'ARGUMENT_DEFINITION'
  | 'INTERFACE'
  | 'UNION'
  | 'ENUM'
  | 'ENUM_VALUE'
  | 'INPUT_OBJECT'
  | 'INPUT_FIELD_DEFINITION'