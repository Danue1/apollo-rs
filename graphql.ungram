// Legend:
//
//   //          -- comment
//   Name =      -- non-terminal definition
//   'ident'     -- token (terminal)
//   A B         -- sequence
//   A | B       -- alternation
//   A*          -- zero or more repetition
//   A?          -- zero or one repetition
//   (A)         -- same as A
//   label:A     -- suggested name for field of AST node

Name =
  'ident'

///////////////////////////
//      INT LITERAL      //
///////////////////////////
// TODO: remove me
// IntValue =
//   IntegerPart
// 
// IntegerPart =
//   NegativeSign? '0'
//   NegativeSign? NonZeroDigit Digit*
// 
// NegativeSign =
//   '-'
// 
// Digit =
//   'int_number'
// 
// // parser needs to omit 'zero' from these digits
// NonZeroDigit =
//   Digit

///////////////////////////
//     FLOAT LITERAL     //
///////////////////////////
// TODO: remove me
// FloatValue =
//   IntegerPart FractionalPart
//   | IntegerPart ExponentPart
//   | IntegerPart FractionalPart ExponentPart
// 
// FractionalPart =
//   '.' Digit*
// 
// ExponentPart =
//   ExponentIndicator Sign? Digit*
// 
// ExponentIndicator =
//   'e'
//   | 'E'
// 
// Sign =
//   '+'
//   | '-'

///////////////////////////
//     STRING LITERAL    //
///////////////////////////
// TODO: remove me
// StringValue =
//   '"' StringCharacter* '"'
//   '"""' BlockStringCharacter* '"""'
// 
// // parser needs to make sure string literal does not have " or line terminators
// StringCharacter =
//   'string'
// 
// // parser should make sure we are not processing unicode and escape characters here
// BlockStringCharacter =
//   'raw_string'

///////////////////////////
//        DOCUMENT       //
///////////////////////////
Document =
  Definition*

Definition =
  ExecutableDefinition
  | TypeSystemDefinition
  | TypeSystemExtension

ExecutableDefinition =
  OperationDefinition
  | FragmentDefinition

OperationDefinition =
  OperationType Name? VariableDefinitions? Directives? SelectionSet
  | SelectionSet

OperationType =
  'query'
  | 'mutation'
  | 'subscription'

SelectionSet =
  '{' Selection* '}'

Selection =
  Field
  | FragmentSpread
  | InlineFragment

Field =
  Alias? Name Arguments? Directives? SelectionSet?

Alias =
  Name ':'

Arguments =
  '(' Argument* ')'

Argument =
  Name ':' Value

FragmentSpread =
  '...' FragmentName Directives?

InlineFragment =
  '...' TypeCondition? Directives? SelectionSet

FragmentDefinition =
  'fragment' FragmentName TypeCondition Directives? SelectionSet

FragmentName =
  Name

TypeCondition =
  'on' NamedType

Value =
  Variable
  | StringValue
  | FloatValue
  | IntValue
  | BooleanValue
  | NullValue
  | EnumValue
  | ListValue
  | ObjectValue

StringValue =
  'string_value'

FloatValue =
  'float_value'

IntValue =
  'int_value'

BooleanValue =
  'true'
  | 'false'

NullValue =
  'null'

EnumValue =
  Name

ListValue =
  '[' ']'
  '[' Value* ']'
  
ObjectValue =
 '{' '}'
 '{' ObjectField* '}'

ObjectField =
  Name ':' Value

VariableDefinitions =
  '(' VariableDefinition* ')'
  
VariableDefinition =
  Variable ':' Type DefaultValue?
  
Variable =
  '$' Name

DefaultValue =
  '=' Value

Type =
  NamedType
  | ListType
  | NonNullType

NamedType =
  Name

ListType =
  '[' Type ']'
  
NonNullType =
  NamedType '!'
  | ListType '!'
  
Directives =
  Directive*

Directive =
  '@' Name Arguments?

TypeSystemDefinition =
  TypeSystemDefinitionKinds

TypeSystemDefinitionKinds =
  SchemaDefinition
  | TypeDefinition
  | DirectiveDefinition

TypeSystemExtension =
  TypeSystemExtensionKinds

TypeSystemExtensionKinds =
  SchemaExtension
  | TypeExtension

SchemaDefinition =
  'schema' Directives? '{' OperationTypeDefinition* '}'

SchemaExtension =
  'extend' 'schema' Directives? '{' OperationTypeDefinition* '}'
  | 'extend' 'schema' Directives

OperationTypeDefinition =
  OperationType ':' NamedType

Description =
  StringValue

TypeDefinition =
  TypeDefinitionKinds

TypeDefinitionKinds =
  ScalarTypeDefinition
  | ObjectTypeDefinition
  | InterfaceTypeDefinition
  | UnionTypeDefinition
  | EnumTypeDefinition
  | InputObjectTypeDefinition

TypeExtension =
  TypeExtensionKinds
 
TypeExtensionKinds =
  ScalarTypeExtension
  | ObjectTypeExtension
  | InterfaceTypeExtension
  | UnionTypeExtension
  | EnumTypeExtension
  | InputObjectTypeExtension

ScalarTypeDefinition =
  Description? 'scalar' Name Directives?

ScalarTypeExtension =
  'extend' 'scalar' Name Directives

ObjectTypeDefinition =
  Description? 'type' Name ImplementsInterfaces? Directives? FieldsDefinition?

ObjectTypeExtension =
  'extend' 'type' Name ImplementsInterfaces? Directives? FieldsDefinition
  | 'extend' 'type' Name ImplementsInterfaces? Directives
  | 'extend' 'type' Name ImplementsInterfaces

ImplementsInterfaces =
  'implements' '&'? NamedType
  | ImplementsInterfaces '&' NamedType

FieldsDefinition =
  '{' FieldDefinition* '}'

FieldDefinition =
  Description? Name ArgumentsDefinition? ':' Type Directives?

ArgumentsDefinition =
  '(' InputValueDefinition* ')'

InputValueDefinition =
  Description? Name ':' Type DefaultValue? Directives?

InterfaceTypeDefinition =
  Description? 'interface' Name Directives? FieldsDefinition?

InterfaceTypeExtension =
  'extend' 'interface' Name Directives? FieldsDefinition
  | 'extend' 'interface' Name Directives

UnionTypeDefinition =
  Description? 'union' Name Directives? UnionMemberTypes?

UnionMemberTypes =
  '=' '|'? NamedType
  | UnionMemberTypes '|' NamedType

UnionTypeExtension =
  'extend' 'union' Name Directives? UnionMemberTypes
  | 'extend' 'union' Name Directives

EnumTypeDefinition =
  Description? 'enum' Name Directives? EnumValuesDefinition?

EnumValuesDefinition =
  '{' EnumValueDefinition* '}'

EnumValueDefinition =
  Description? EnumValue Directives?

EnumTypeExtension =
  'extend' 'enum' Name Directives? EnumValuesDefinition
  | 'extend' 'enum' Name Directives

InputObjectTypeDefinition =
  Description? 'input' Name Directives? InputFieldsDefinition?

InputFieldsDefinition =
  '{' InputValueDefinition* '}'

InputObjectTypeExtension =
  'extend' 'input' Name Directives? InputFieldsDefinition
  | 'extend' 'input' Name Directives?

DirectiveDefinition =
  Description? 'directive' '@' Name ArgumentsDefinition? 'on' DirectiveLocations

DirectiveLocations =
  '|'? DirectiveLocation
  | DirectiveLocations '|' DirectiveLocation

DirectiveLocation =
  ExecutableDirectiveLocation
  TypeSystemDirectiveLocation

ExecutableDirectiveLocation =
  'QUERY'
  | 'MUTATION'
  | 'SUBSCRIPTION'
  | 'FIELD'
  | 'FRAGMENT_DEFINITION'
  | 'FRAGMENT_SPREAD'
  | 'INLINE_FRAGMENT'

TypeSystemDirectiveLocation =
  'SCHEMA'
  | 'scalar'
  | 'OBJECT'
  | 'FIELD_DEFINITION'
  | 'ARGUMENT_DEFINITION'
  | 'INTERFACE'
  | 'UNION'
  | 'ENUM'
  | 'ENUM_VALUE'
  | 'INPUT_OBJECT'
  | 'INPUT_FIELD_DEFINITION'